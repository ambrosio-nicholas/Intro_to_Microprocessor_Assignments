' {$STAMP BS2} ' Stamp directive
' {$PBASIC 2.5} ' PBASIC Directive
hours VAR Byte ' Stores hours
minutes VAR Byte ' Stores minutes
seconds VAR Byte ' Stores seconds

LcdPin PIN 14 ' LCD I/O pin
T9600 CON 84 ' True, 8-bits, no parity, 9600
LcdCls CON 12 ' Form feed -> clear screen

LcdCr CON 13 ' Carriage return
LcdOff CON 21 ' Turns display off
LcdOn CON 22 ' Turns display on
Line0 CON 128 ' Line 0, character 0
Line1 CON 148 ' Line 1, character 0

SEROUT LcdPin, T9600, [LcdOn, LcdCls] ' Start LCD & clear display

PAUSE 5 ' Pause 5 ms for clear display

SEROUT LcdPin, T9600, ["Time Elapsed...", LcdCr] ' Text + carriage return

SEROUT LcdPin, T9600, ["  h    m    s"] ' Text on second line

DO ' Main Routine
  ' Calculate hours, minutes, seconds
  IF seconds = 60 THEN seconds = 0: minutes = minutes + 1

  IF minutes = 60 THEN minutes = 0: hours = hours + 1

  IF hours = 24 THEN hours = 0

  ' Display digits on LCD on Line 1. The values 148, 153, 158
  ' place the cursor at character 0, 5, and 10 for the time values.
  SEROUT LcdPin, T9600, [Line1, DEC2 hours, Line1 + 5, DEC2 minutes, Line1 + 10, DEC2 seconds ]

  PAUSE 991 ' Pause + overhead ~ 1 second

  seconds = seconds + 1 ' Increment second counter
LOOP